						ASSUME	CS:CODE,DS:CODE
						PAGE	100,160

VERSION_STRING			EQU		"VERSION 20220502"

						.386p
CODE					SEGMENT	USE16

						INCLUDE		IODEF.ASM
						INCLUDE		DEF.ASM

MAIN					PROC
						; ****ing MASM kindly inserts NOP after JMP and messes up the offsets.  Good job, Microsoft.
						DB		0EBH,(REALMAIN-@f)  ; JMP		REALMAIN
@@:
						DB		"RESCUE"
												; Offsets must be consisntent with DEF.H
CONTROL_FLAGS			DW		0				; +8
YSSCSICD_LBA			DD		0				; +10  For CD and HD IPL
YSSCSICD_SECTOR_COUNT	DB		24				; +14  For CD and HD IPL
						DB		0				; +15  Not used at this time.
SELF_LBA				DD		0				; +16  Loader LBA (Set only when booting from HD)

INCOMING_BX				DW		0
LOADER_BIOS_DEVICE_ID	DB		0
YSSCSICD_LOADED			DB		0

REALMAIN:
; Incoming BL:Device Type   1:HD/CD  2:FD  4:ICM  8:CD
; Incoming BH:Unit Number
						CLI
						PUSH	CS
						POP		DS

						MOV		BP,CS
						SUB		BP,1000H
						MOV		SS,BP
						MOV		SP,0FFFEH ; IO.SYS will reset the stack pointer anyway.

						MOV		CS:[INCOMING_BX],BX
						AND		BX,0F0FH
						CMP		BL,1		; HD/CD?
						JNE		SHORT @f
						OR		BH,0B0H
						JMP		SHORT DEVICE_ID_SET
@@:
						CMP		BL,2		; FD?
						JNE		SHORT @f
						OR		BH,020H
						JMP		SHORT DEVICE_ID_SET

@@:
						CMP		BL,4		; ICM?
						JNE		SHORT @f
						MOV		BH,4AH
						JMP		SHORT DEVICE_ID_SET

@@:
						XOR		BX,BX

DEVICE_ID_SET:
						MOV		CS:[LOADER_BIOS_DEVICE_ID],BH


						CALL	INITGRAPHICS
						CALL	VRAMMODE
						CALL	CLS_HIGH
						CALL	RESETPALETTE16

						CALL	CRTC640X480_640X480

						CALL	LISTSCSI
						CALL	FIND_LAST_SCSI_CD
						MOV		CS:[SCSI_ID_CDROM],AL

				; CALL	TSUGARU_DEBUG_BREAK

						MOV		AL,15
						CALL	COLOR
						CALL	DEMOSPLASH_MESSAGE

						TEXTLOCATION31K	1,1
						MOV		SI,OFFSET MESSAGE_LINE0
						CALL	WRSTRING_WHITE_HIGH

						TEXTLOCATION31K	30H,01H
						MOV		SI,OFFSET MESSAGE_MACHINE_ID
						CALL	WRSTRING_WHITE_HIGH

						IN		AL,0031H	; Machine ID High
						CALL	ITOX8
						XCHG	AH,AL
						MOV		WORD PTR CS:[UNCHARTED_TERRITORY],AX

						IN		AL,0030H	; Machine ID Low
						CALL	ITOX8
						XCHG	AH,AL
						MOV		WORD PTR CS:[UNCHARTED_TERRITORY+2],AX

						MOV		BYTE PTR CS:[UNCHARTED_TERRITORY+4],0
						TEXTLOCATION31K	3CH,01H
						MOV		SI,OFFSET UNCHARTED_TERRITORY
						CALL	WRSTRING_WHITE_HIGH


						TEXTLOCATION31K	01H,02H
						MOV		SI,OFFSET MESSAGE_LINE1
						CALL	WRSTRING_WHITE_HIGH

						TEXTLOCATION31K	01H,03H
						MOV		SI,OFFSET MESSAGE_LINE2
						CALL	WRSTRING_WHITE_HIGH

						MOV		EDI,288
						CALL	WRAOMORI_HIGH

						CALL	LOAD_YSSCSICD

						TEST	WORD PTR [CONTROL_FLAGS],CONTROL_FLAG_BOOT_MENU
						JNE		MAINMENU

						CALL	BOOT_FROM_SCSI_CD

						JMP		MAINMENU


MAIN					ENDP

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



DEMOSPLASH_MESSAGE		PROC
						MOV		AX,280AH
						MOV		SI,OFFSET MSG_DEMOSPLASH
						CALL	DRAW_TEXT31K
						RET
DEMOSPLASH_MESSAGE		ENDP

DEMOSPLASH_MESSAGE_MOVABLE		PROC
						MOV		SI,OFFSET MSG_DEMOSPLASH
						CALL	DRAW_TEXT31K
						RET
DEMOSPLASH_MESSAGE_MOVABLE		ENDP


MSG_DEMOSPLASH			DB		"PLEASE JOIN US AT DEMOSPLASH 2022",0
						DB		"AND 2023, 2024, ...",0
						DB		"http://demosplash.org",0
						DB		0FFH,0FFH



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



HIROSAKI_MESSAGE_MOVABLE		PROC
						MOV		SI,OFFSET MSG_HIROSAKI
						CALL	DRAW_TEXT31K
						RET
HIROSAKI_MESSAGE_MOVABLE		ENDP

MSG_HIROSAKI			DB		"VISIT HIROSAKI CITY",0
						DB		"IN AOMORI PREFECTURE, JAPAN",0
						DB		"AKA 'TSUGARU' REGION",0
						DB		"WHEN THIS PANDEMIC IS OVER.",0
						DB		"BEAUTIFUL CHERRY BLOSSOMS IN APRIL",0
						DB		"FAMOUS NEPUTA FESTIVAL IN AUGUST",0
						DB		"GORGEOUS IWAKI MOUNTAIN",0
						DB		"CRISPY JAPAN'S BEST APPLES",0
						DB		"CUTE HIROSAKI CASTLE",0
						DB		0FFH,0FFH



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



						INCLUDE		UTIL.ASM
						INCLUDE		DRAWFONT.ASM
						INCLUDE		MEMBANK.ASM
						INCLUDE		../SCSILIB/SCSIIO.ASM
						INCLUDE		../RS232C/RS232C.ASM
						INCLUDE		../RS232C/XMODEM.ASM
						INCLUDE		CRTC.ASM
						INCLUDE		../MISCLIB/SERIAL.ASM
						INCLUDE		../MISCLIB/RDHIGH.ASM
						INCLUDE		../MISCLIB/GDT.ASM
						INCLUDE		SCSIUTIL.ASM
						INCLUDE		CDBOOT.ASM
						INCLUDE		LOADYSSC.ASM
						INCLUDE		TSUGARU.ASM
						INCLUDE		UI.ASM
						INCLUDE		HID_IO.ASM

MESSAGE_LINE0			DB	"FM TOWNS RESCUE BOOT LOADER BY CAPTAINYS",0
MESSAGE_LINE1			DB	VERSION_STRING,0
MESSAGE_LINE2			DB	"http://www.ysflight.com",0
MESSAGE_MACHINE_ID		DB	"MACHINE ID:",0

						INCLUDE		MAINMENU.ASM
						INCLUDE		RS232MNU.ASM
						INCLUDE		CMOSMENU.ASM
						INCLUDE		CMOSRST.ASM
						INCLUDE		IPLDMENU.ASM
						INCLUDE		BOOTMENU.ASM
						INCLUDE		INTRCEPT.ASM
						INCLUDE		PATCH.ASM
						INCLUDE		VRAM.ASM
						INCLUDE		../RESOURCE/AOMORI.ASM
						INCLUDE		BUFFERS.ASM


						; After the end of the program.
						; Can be used as a large buffer
UNCHARTED_TERRITORY		DB		0



CODE					ENDS

						END		MAIN
