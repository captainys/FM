TOWNSIO_VIDEO_OUT_ADDR	EQU		0448H
TOWNSIO_VIDEO_OUT_DATA	EQU		044AH

TOWNSIO_PALETTE_CODE	EQU		0FD90H



RESETPALETTE16			PROC
						PUSH	SI
						PUSH	DX
						PUSH	AX

						MOV		DX,TOWNSIO_VIDEO_OUT_ADDR
						MOV		AL,1	; ADDRESS 1
						OUT		DX,AL

						MOV		DX,TOWNSIO_VIDEO_OUT_DATA
						MOV		AL,29H	; Layer 1 16-color Palette Priority=Layer 1
						OUT		DX,AL
						CALL	RESETPALETTE16_PAGE

						MOV		DX,TOWNSIO_VIDEO_OUT_DATA
						MOV		AL,09H	; Layer 1 16-color Palette Priority=Layer 1
						OUT		DX,AL
						CALL	RESETPALETTE16_PAGE

						POP		AX
						POP		DX
						POP		SI
						RET
RESETPALETTE16			ENDP



RESETPALETTE16_PAGE		PROC
						MOV		DX,TOWNSIO_PALETTE_CODE
						MOV		SI,OFFSET PALETTE16_DATA
						MOV		AH,64
@@:
						MOV		AL,CS:[SI]
						INC		SI
						OUT		DX,AL
						ADD		DL,2
						AND		DL,0F7H
						DEC		AH
						JNE		@B

						RET
RESETPALETTE16_PAGE		ENDP

;								CODE,   B,   R,   G
PALETTE16_DATA			DB		0,   000H,000H,000H
						DB		1,   070H,000H,000H
						DB		2,   000H,070H,000H
						DB		3,   070H,070H,000H
						DB		4,   000H,000H,070H
						DB		5,   070H,000H,070H
						DB		6,   000H,070H,070H
						DB		7,   070H,070H,070H
						DB		8,   000H,000H,000H
						DB		9,   0F0H,060H,060H
						DB		10,  000H,0F0H,000H
						DB		11,  0F0H,0F0H,000H
						DB		12,  000H,000H,0F0H
						DB		13,  0F0H,000H,0F0H
						DB		14,  000H,0F0H,0F0H
						DB		15,  0F0H,0F0H,0F0H



CRTC640X480_640X480MOD_REG_00_01	DW	0060H,02C0H										; +00H
CRTC640X480_640X480MOD_REG_02_1F	DW	031FH,0000H,0004H,0000H							; +04H
									DW	0419H,008AH,030AH,008AH,030AH,0046H,0406H,0046H	; +08H
									DW	0406H,0000H,008AH,0000H,0050H,0000H,008AH,0000H	; +10H
									DW	0050H,0058H,0001H,0000H,803FH,0002H,0000H,0192H	; +18H
CRTC640X480_640X480MOD_SIFTER		DB	15H,08H,00H,00H
; 14H 0080H->0050H
; 18H 0080H->0050H
; 1CH 800FH->803FH

; Input DS:SI Register/Sifter table.
CRTC_SETREG				PROC
						MOV		DH,04H

						MOV		BX,0
						MOV		CX,2
						CALL	SET_CRTCREG_LOOP

						MOV		BX,4
						MOV		CX,1CH
						CALL	SET_CRTCREG_LOOP

						MOV		AH,0
						MOV		CX,4
SET_CRTCSIFTER_LOOP:
						MOV		AL,AH
						MOV		DL,48H
						OUT		DX,AL
						INC		AH
						LODSB
						MOV		DL,4AH
						OUT		DX,AL
						LOOP	SET_CRTCSIFTER_LOOP

						RET

SET_CRTCREG_LOOP:
						MOV		AX,BX
						MOV		DL,40H
						OUT		DX,AL
						LODSW
						MOV		DL,42H
						OUT		DX,AX
						INC		BX
						LOOP	SET_CRTCREG_LOOP
						RET

CRTC_SETREG				ENDP


; AX reg
; BX value
CRTC_WRITEREG			PROC
						MOV		DX,440H
						OUT		DX,AX
						MOV		DL,42H
						MOV		AX,BX
						OUT		DX,AX
						RET
CRTC_WRITEREG			ENDP


CRTC640X480_640X480MOD	PROC
						MOV		SI,OFFSET CRTC640X480_640X480MOD_REG_00_01
						PUSH	CS
						POP		DS
						CALL	CRTC_SETREG

						; MOV		AX,14H
						; MOV		BX,50H
						; CALL	CRTC_WRITEREG
						; MOV		AX,18H
						; MOV		BX,50H
						; CALL	CRTC_WRITEREG
						RET
CRTC640X480_640X480MOD	ENDP
