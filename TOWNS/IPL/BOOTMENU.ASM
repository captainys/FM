BOOTMENU_ITEMNUM_RETURN_TO_MAINMENU	EQU		0
BOOTMENU_ITEMNUM_ENABLE_YSSCSICD_HD_FD	EQU		1
BOOTMENU_ITEMNUM_INTERNAL_CD	EQU		2
BOOTMENU_ITEMNUM_FD0			EQU		3
BOOTMENU_ITEMNUM_SCSI_CD		EQU		5
BOOTMENU_ITEMNUM_SCSI_HDD_0		EQU		6
BOOTMENU_ITEMNUM_PARTITION_0	EQU		11



INSTRUCTION_MESSAGE		DB		"USE GAMEPAD0 UP/DOWN TO MOVE CURSOR, A-BUTTON TO SELECT BOOT DEVICE",0
ASKRELEASE_MESSAGE		DB		"PLEASE RELEASE PAD0 BUTTONS                                        ",0

SHOW_REMOVE_INTERNAL_CD_MSG		DB		0
REMOVE_INTERNAL_CD_MSG	DB		"**** REMOVE DISC FROM INTERNAL CD DRIVE WHEN BOOT FROM SCSI CD ****",0

READERROR_MESSAGE		DB		"FAILED TO READ BOOT SECTOR",0
NOT_IPL4_MESSAGE		DB		"NOT A BOOTABLE MEDIA      ",0

NUM_BOOTMENU_OPTIONS	DB		15

BOOTMENU_ITEM_BUFFER	DB		"BACK TO MAIN MENU",0
BOOTMENU_ITEM_ENYSSCSICD	DB		"[ ] ENABLE SCSICD IN HD/FD BOOT",0
						DB		"INTERNAL CD DRIVE",0
						DB		"FLOPPY DRIVE A",0
						DB		"FLOPPY DRIVE B",0
						DB		"SCSI CD DRIVE",0
						DB		"SCSI HDD ID=0",0
						DB		"SCSI HDD ID=1",0
						DB		"SCSI HDD ID=2",0
						DB		"SCSI HDD ID=3",0
						DB		"SCSI HDD ID=4",0
BOOTMENU_ITEM_BUFFER_END:

BOOTMENU_ITEM_PARTITION	DB		"HDD PARTITION 0 ["
BOOTMENU_ITEM_PARTITION_END:

ARROW_ERASE				DB		"   ",0
ARROW_DRAW				DB		">>>",0

AUTOBOOT				DB		1

AUTOBOOT_PREV_VSYNC		DB		0
AUTOBOOT_COUNTER		DW		721		; 12*VSYNC_PER_SEC+1

AUTOBOOT_MSG			DB		"AUTO BOOT IN "
AUTOBOOT_NUMBER			DB		"00",0



BOOTMENU				PROC

						CALL	CLEAR_FIVE_BELOW

						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_REMEMBER_BOOT_CHOICE
						JNE		SHORT @f
						CALL	LOAD_OPTION
@@:

						CALL	BOOTMENU_SETUP

BOOTMENU_OUTER_LOOP:
						CALL	MENU_WAIT_PAD_RELEASE

						MOV		AX,DISPLACED_DEMOSPLASh_MESSAGE_LOCATION
						CALL	DEMOSPLASH_MESSAGE_MOVABLE

						MOV		AX,DISPLACED_DEMOSPLASh_MESSAGE_LOCATION
						ADD		AL,5
						CALL	HIROSAKI_MESSAGE_MOVABLE

						TEXTLOCATION	INSTRUCTION_X,INSTRUCTION_Y
						MOV		SI,OFFSET INSTRUCTION_MESSAGE
						CALL	PRINT_TALL

						CMP		BYTE PTR CS:[SHOW_REMOVE_INTERNAL_CD_MSG],0
						JE		SHORT @f
						MOV		AL,0AH
						CALL	COLOR
						TEXTLOCATION	INSTRUCTION_X,INSTRUCTION_Y+1
						MOV		SI,OFFSET REMOVE_INTERNAL_CD_MSG
						CALL	PRINT_TALL
						MOV		AL,0FH
						CALL	COLOR
@@:

						MOV		SI,OFFSET MENU_EXPAND_BUF
						MOVZX	CX,CS:[NUM_BOOTMENU_OPTIONS]
						CALL	DRAWMENU2

						MOV		CL,CS:[NUM_BOOTMENU_OPTIONS]
						MOV		AL,CS:[BOOTMENU_SELECTION]
						CALL	DRAWARROW


						; Wait for PAD while count down for AUTOBOOT >>
BOOTMENU_PAD_LOOP:
						CMP		BYTE PTR CS:[AUTOBOOT],0
						JE		BOOTMENU_PAD_READ
						MOV		DX,IO_VSYNC
						IN		AL,DX
						AND		AL,1
						MOV		AH,CS:[AUTOBOOT_PREV_VSYNC]
						MOV		CS:[AUTOBOOT_PREV_VSYNC],AL
						XOR		AH,1
						AND		AH,AL
						JE		BOOTMENU_PAD_READ

						DEC		WORD PTR CS:[AUTOBOOT_COUNTER]
						MOV		AL,0FH	; Virtually Press A button
						CMP		WORD PTR CS:[AUTOBOOT_COUNTER],0
						JE		BOOTMENU_PAD_IS_IN

						MOV		AX,CS:[AUTOBOOT_COUNTER]
						MOV		DL,60
						DIV		DL
						AND		AH,AH
						JNE		BOOTMENU_PAD_READ

						CALL	ITOX8
						XCHG	AH,AL
						MOV		WORD PTR CS:[AUTOBOOT_NUMBER],AX

						MOV		AL,0CH
						CALL	COLOR

						TEXTLOCATION	AUTOBOOT_MESSAGE_X,AUTOBOOT_MESSAGE_Y
						MOV		SI,OFFSET AUTOBOOT_MSG
						CALL	PRINT_TALL

						MOV		AX,FIRST_ARROW_LOCATION
						ADD		AL,CS:[BOOTMENU_SELECTION]
						CALL	LOCATE
						MOV		SI,OFFSET AUTOBOOT_NUMBER
						CALL	PRINT_TALL

						MOV		AL,0FH
						CALL	COLOR

BOOTMENU_PAD_READ:
						CALL	READ_PADA
						CMP		AL,3FH
						JE		BOOTMENU_PAD_LOOP
						; Wait for PAD while count down for AUTOBOOT <<



BOOTMENU_PAD_IS_IN:
						MOV		BYTE PTR CS:[AUTOBOOT],0	; Once pad is moved, no more auto boot
						PUSH	AX
						TEXTLOCATION	0,AUTOBOOT_MESSAGE_Y
						CALL	ERASE_LINE_TALL
						POP		AX

						MOV		AH,CS:[BOOTMENU_SELECTION]
						MOV		CH,CS:[NUM_BOOTMENU_OPTIONS]
						CALL	MOVE_ARROW_BY_PAD
						MOV		CS:[BOOTMENU_SELECTION],AH

						AND		AL,30H
						CMP		AL,30H
						JE		BOOTMENU_OUTER_LOOP

						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],0
						JE		BACK_TO_MAIN_MENU

						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_REMEMBER_BOOT_CHOICE
						JNE		SHORT @f
						CALL	SAVE_OPTION
@@:
						CALL	CLEAR_FIVE_BELOW
						CALL	TRYBOOT
						CALL	CLEAR_FIVE_BELOW

						JMP		BOOTMENU_OUTER_LOOP

BACK_TO_MAIN_MENU:
						RET

BOOTMENU				ENDP



; Input
;   DS:SI	Menu Top
;   CX		Number of Items
; Output
;   CX,SI   Destroyed
DRAWMENU2				PROC
						MOV		AX,FIRST_MENU_ITEM_LOCATION
DRAWMENU2_OUTER_LOOP:
						PUSH	CX
						PUSH	AX
						PUSH	SI
						CALL	LOCATE
						CALL	PRINT_TALL
						POP		SI

						CALL	SKIP_CSTR_DS_SI

						POP		AX
						POP		CX
						INC		AL
						LOOP	DRAWMENU2_OUTER_LOOP

						RET
DRAWMENU2				ENDP



; Input
;   DS:SI   C-String
; Output
;   DS:SI   Next C-String
SKIP_CSTR_DS_SI			PROC
						INC		SI
						CMP		BYTE PTR [SI],0
						JNE		SKIP_CSTR_DS_SI
						INC		SI
						RET
SKIP_CSTR_DS_SI			ENDP



BOOTMENU_SETUP			PROC

						PUSH	CS
						POP		ES

						MOV		SI,OFFSET BOOTMENU_ITEM_BUFFER
						MOV		DI,OFFSET MENU_EXPAND_BUF
						MOV		CX,BOOTMENU_ITEM_BUFFER_END-BOOTMENU_ITEM_BUFFER
						REP MOVSB

						MOV		CS:[NUM_BOOTMENU_OPTIONS],BOOTMENU_ITEMNUM_PARTITION_0

						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_SCAN_PARTITION
						JNE		BOOTMENU_SETUP_DONE


						PUSH	DI

						MOV		BX,CS:[INCOMING_BX]

						MOV		AH,5
						MOV		AL,BH
						AND		AL, 07H
						OR		AL,0B0H

						XOR		CX,CX
						MOV		DX,1	; LBA=1 Partition Table

						MOV		BX,1

						MOV		DI,OFFSET PARTITION_TABLE

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						MOV		SI,OFFSET PARTITION_TABLE+20H

						POP		DI


						XOR		CX,CX
BOOTMENU_SETUP_OUTER_LOOP:
						CMP		BYTE PTR CS:[SI+1],0
						JE		BOOTMENU_SETUP_DONE
						CMP		BYTE PTR CS:[SI+1],90H	; I don't know about 90H, but HD Boot Loader stops checking when it see 90H
						JE		BOOTMENU_SETUP_DONE

						PUSH	SI
						PUSH	CX

						MOV		SI,OFFSET BOOTMENU_ITEM_PARTITION
						MOV		CX,BOOTMENU_ITEM_PARTITION_END-BOOTMENU_ITEM_PARTITION
						REP		MOVSB

						POP		CX
						POP		SI
						PUSH	SI
						PUSH	CX

						ADD		CL,'0'
						MOV		[DI-3],CL

						MOV		CX,16
						ADD		SI,20H
						REP		MOVSB
						MOV		AL,']'
						STOSB
						XOR		AL,AL
						STOSB

						POP		CX
						POP		SI

						INC		CS:[NUM_BOOTMENU_OPTIONS]
						ADD		SI,030H
						CMP		CX,5
						JNE		BOOTMENU_SETUP_OUTER_LOOP

BOOTMENU_SETUP_DONE:

						MOV		CL,CS:[BOOTMENU_SELECTION]
						CMP		CL,CS:[NUM_BOOTMENU_OPTIONS]
						JAE		SHORT @f
						MOV		CS:[BOOTMENU_SELECTION],CL

@@:
						CALL	RESET_MENU_INSTALL_YSSCSICD_HDFD

						RET

BOOTMENU_SETUP			ENDP



BOOTMENU_PROMPT_CD_CHANGE	PROC
						CALL	MENU_WAIT_PAD_RELEASE

						CALL	CLEAR_FIVE_BELOW
						MOV		AX,050AH
						MOV		SI,OFFSET CD_CHANGE_MESSAGE
						CALL	DRAW_TEXT
@@:
						CALL	READ_PADA
						CMP		AL,3FH
						JE		@b

						RET
BOOTMENU_PROMPT_CD_CHANGE	ENDP

CD_CHANGE_MESSAGE		DB		"INSERT TOWNS-APP CD IN THE SCSI CD DRIVE, AND",0
						DB		"PRESS A GAMEPAD0 BUTTON.",0
						DB		0
						DB		0
						DB		"NOTE: NOT ALL TOWNS-APPs CAN BE STARTED FROM",0
						DB		"      THE SCSI CD DRIVE.  SOME REQUIRE A PATCH.",0
						DB		0FFH,0FFH



LOAD_OPTION				PROC

						MOV		BX,CS:[INCOMING_BX]
						; BL:Device Type   1:SCSI  2:FD  8:CD
						CMP		BL,1
						JNE		SHORT LOAD_OPTION_EXIT

						MOV		AH,5
						MOV		AL,BH
						AND		AL, 07H
						OR		AL,0B0H

						MOV		ECX,CS:[SELF_LBA]
						ADD		ECX,((OFFSET BOOTMENU_SELECTION)-(OFFSET MAIN))/512
						MOV		DX,CX	; DX=LBA Low
						SHR		ECX,16	; CX=LBA High
						XOR		CH,CH	; CH needs to be zero

						MOV		BX,1

						MOV		DI,OFFSET BOOTMENU_SELECTION

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH
LOAD_OPTION_EXIT:
						RET

LOAD_OPTION				ENDP



SAVE_OPTION				PROC

						MOV		BX,CS:[INCOMING_BX]
						; BL:Device Type   1:SCSI  2:FD  8:CD  4:ICM

						CMP		BL,4
						JE		SHORT SAVE_OPTION_ICM

						CMP		BL,1
						JNE		SHORT SAVE_OPTION_EXIT


						MOV		AH,6	; Write sector (Question is if SYSROM supports this command)
						MOV		AL,BH
						AND		AL, 07H
						OR		AL,0B0H

						MOV		ECX,CS:[SELF_LBA]
						ADD		ECX,((OFFSET BOOTMENU_SELECTION)-(OFFSET MAIN))/512
						MOV		DX,CX	; DX=LBA Low
						SHR		ECX,16	; CX=LBA High
						XOR		CH,CH	; CH needs to be zero

						MOV		BX,1

						MOV		DI,OFFSET BOOTMENU_SELECTION

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH
SAVE_OPTION_EXIT:
						RET


SAVE_OPTION_ICM:
						MOV		DI,OFFSET BOOTMENU_SELECTION
						MOV		AL,CS:[DI]
						CALL	ICM_WRITE_SETTING_AL

						MOV		DI,OFFSET INSTALL_YSSCSICD_HDFD
						MOV		AL,CS:[DI]
						CALL	ICM_WRITE_SETTING_AL
						RET



SAVE_OPTION				ENDP



MENU_WAIT_PAD_RELEASE	PROC
						TEXTLOCATION	INSTRUCTION_X,INSTRUCTION_Y
						MOV		SI,OFFSET ASKRELEASE_MESSAGE
						CALL	PRINT_TALL

						CALL	WAIT_PADA_RELEASE

						TEXTLOCATION	INSTRUCTION_X,INSTRUCTION_Y
						CALL	ERASE_LINE_TALL

						RET
MENU_WAIT_PAD_RELEASE	ENDP



; Input
;   AL		Selection
;   CL		Number of menu items
DRAWARROW				PROC
						PUSH	AX

						MOV		AX,FIRST_ARROW_LOCATION
						AND		CH,CH

@@:
						PUSH	AX
						PUSH	CX
						CALL	LOCATE
						MOV		SI,OFFSET ARROW_ERASE
						CALL	PRINT_TALL
						POP		CX
						POP		AX
						INC		AL
						LOOP	@b

						MOV		AL,0CH
						CALL	COLOR

						POP		CX
						MOV		AX,FIRST_ARROW_LOCATION
						ADD		AL,CL
						CALL	LOCATE
						MOV		SI,OFFSET ARROW_DRAW
						CALL	PRINT_TALL

						MOV		AL,0FH
						CALL	COLOR

						RET

DRAWARROW				ENDP



TRYBOOT					PROC
						AND		CS:[CONTROL_FLAGS],(NOT CONTROL_FLAG_DONT_INSTALL_YSSCSICD)
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_RETURN_TO_MAINMENU
						JNE		SHORT @f
						RET


@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_ENABLE_YSSCSICD_HD_FD
						JNE		SHORT @f
						CALL	TOGGLE_ENABLE_YSSCSICD_HD_FD
						RET


@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_SCSI_CD
						JNE		TRYBOOT_NOT_FROM_SCSI_CD
						TEST	CS:[CONTROL_FLAGS],CONTROL_FLAG_PROMPT_CD_CHANGE
						JE		SHORT @f
						CALL	BOOTMENU_PROMPT_CD_CHANGE
@@:
						CALL	CHECK_INTERNAL_CD_READY
						JC		SHORT @f
						MOV		BYTE PTR CS:[SHOW_REMOVE_INTERNAL_CD_MSG],0ffh
						RET

@@:
						CALL	BOOT_FROM_SCSI_CD
						RET


TRYBOOT_NOT_FROM_SCSI_CD:
						XOR		DX,DX
						XOR		CX,CX

						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_INTERNAL_CD
						JNE		SHORT @f

						; Boot from Internal CD
						MOV		AL,0C0H
						MOV		BX,0008H
						OR		CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_INSTALL_YSSCSICD
						JMP		TRYBOOT_FROMDEVICE

@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_SCSI_HDD_0
						JAE		SHORT @f

						; Boot from Floppy Disk
						CALL	SETUP_CONTROL_FLAGS_HD_FD_BOOT

						MOV		AL,CS:[BOOTMENU_SELECTION]
						SUB		AL,BOOTMENU_ITEMNUM_FD0
						OR		AL,20H
						MOV		AH,3  ; Restore
						XOR		CH,CH
						DB		9AH		; CALLF FFFB:0014
						DW		0014H,0FFFBH


						MOV		BH,CS:[BOOTMENU_SELECTION]
						SUB		BH,BOOTMENU_ITEMNUM_FD0
						MOV		BL,02H

						MOV		AL,BH
						OR		AL,20H
						MOV		DL,1		; Floppy Disk Sector starts at 1.

						JMP		TRYBOOT_FROMDEVICE

@@:
						CMP		BYTE PTR CS:[BOOTMENU_SELECTION],BOOTMENU_ITEMNUM_PARTITION_0
						JAE		SHORT @f

						; Boot from a SCSI HDD
						CALL	SETUP_CONTROL_FLAGS_HD_FD_BOOT

						MOV		BH,CS:[BOOTMENU_SELECTION]
						SUB		BH,BOOTMENU_ITEMNUM_SCSI_HDD_0
						MOV		BL,1

						MOV		AL,BH
						OR		AL,0B0H
						JMP		SHORT TRYBOOT_FROMDEVICE

@@:
						; Boot from Hard-Disk Partition
						CALL	SETUP_CONTROL_FLAGS_HD_FD_BOOT

						CALL	SETUP_PARTITION_TABLE_B100_SEGMENT

						MOV		AL,CS:[BOOTMENU_SELECTION]
						SUB		AL,BOOTMENU_ITEMNUM_PARTITION_0
						MOV		AH,30H
						MUL		AH
						ADD		AX,20H
						MOV		SI,AX

						MOV		DX,WORD PTR CS:[SI+PARTITION_TABLE+2]	; LBA Low
						MOV		CL,BYTE PTR CS:[SI+PARTITION_TABLE+4]	; LBA High
						XOR		CH,CH

						MOV		BX,CS:[INCOMING_BX]
						AND		BH,7

						MOV		AL,BH
						OR		AL,0B0H

						JMP		SHORT TRYBOOT_FROMDEVICE
TRYBOOT					ENDP





; To tell Fujitsu's boot sector which partition it is booting from,
; partition table needs to be loaded from B100:0000, and
; boot-partition flag needs to be set according to the selected
; partition.
SETUP_PARTITION_TABLE_B100_SEGMENT	PROC
						PUSH	DS
						PUSH	ES


						MOV		AX,CS
						MOV		DS,AX
						MOV		AX,0B100H
						MOV		ES,AX

						MOV		SI,OFFSET PARTITION_TABLE
						XOR		DI,DI
						MOV		CX,512
						REP MOVSB


						MOV		DI,0020H
						MOV		CX,10
						XOR		AL,AL
@@:
						MOV		BYTE PTR ES:[DI],0	; Tentatively clear boot-partition flag
						ADD		DI,0030H
						LOOP	@b


						MOV		AL,CS:[BOOTMENU_SELECTION]
						SUB		AL,BOOTMENU_ITEMNUM_PARTITION_0
						MOV		AH,30H
						MUL		AH
						ADD		AX,20H
						MOV		DI,AX
						MOV		BYTE PTR ES:[DI],0FFH	; Boot-partition flag

						POP		ES
						POP		DS
						RET
SETUP_PARTITION_TABLE_B100_SEGMENT	ENDP



; Input
;   AL   	Boot Device Code for BIOS
;	CL|DX	Boot Sector as Specified by BIOS
;	BX		Boot Device ID
; 		 BL:Device Type   1:HD/CD  2:FD  8:CD
TRYBOOT_FROMDEVICE		PROC

						PUSH	DS
						PUSH	BX

						MOV		BX,0B000H
						MOV		DS,BX

						MOV		DWORD PTR DS:[0],0

						MOV		AH,05H	; Read Sector
						MOV		CH,0
						MOV		BX,4	; 4 sectors
						MOV		DI,0	; Offset=0

						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						DB		9AH		; CALLF FFFB:0014
						DW		 0014H
						DW		0FFFBH

						MOV		EAX,DS:[0]
						POP		BX
						POP		DS
						JB		TRYBOOT_READERROR

						CMP		EAX,344C5049H	; "IPL4"
						JNE		TRYBOOT_NOT_IPL4

						MOV		AX,0FFFFH

						; Uncomment following DB and DWs and comment CALL SETUP_INTERCEPT_AND_GO
						; for simple boot without patching IPL.
						; How can I write CALL 		FAR PTR 0FFFBH:0014H
						;DB		9AH		; CALLF B000:0004
						;DW		 0004H
						;DW		0B000H
						CALL	SETUP_INTERCEPT_AND_GO

						PUSH	CS
						POP		DS

						RET

TRYBOOT_READERROR:
						TEXTLOCATION	ERRORMESSAGE_X,ERRORMESSAGE_Y
						MOV		SI,OFFSET READERROR_MESSAGE
						CALL	PRINT_TALL
						RET

TRYBOOT_NOT_IPL4:
						TEXTLOCATION	ERRORMESSAGE_X,ERRORMESSAGE_Y
						MOV		SI,OFFSET NOT_IPL4_MESSAGE
						CALL	PRINT_TALL
						RET


TRYBOOT_FROMDEVICE		ENDP





TOGGLE_ENABLE_YSSCSICD_HD_FD	PROC
						MOV		AL,1
						SUB		AL,CS:[INSTALL_YSSCSICD_HDFD]
						MOV		CS:[INSTALL_YSSCSICD_HDFD],AL
						JMP		RESET_MENU_INSTALL_YSSCSICD_HDFD
TOGGLE_ENABLE_YSSCSICD_HD_FD	ENDP





RESET_MENU_INSTALL_YSSCSICD_HDFD	PROC
						SUB		AL,CS:[INSTALL_YSSCSICD_HDFD]
						AND		AL,AL
						MOV		AL,' '
						JE		@f
						MOV		AL,'X'
@@:
						MOV		CS:[MENU_EXPAND_BUF+(BOOTMENU_ITEM_ENYSSCSICD-BOOTMENU_ITEM_BUFFER)+1],AL
						RET
RESET_MENU_INSTALL_YSSCSICD_HDFD	ENDP




SETUP_CONTROL_FLAGS_HD_FD_BOOT		PROC
						CMP		BYTE PTR CS:[INSTALL_YSSCSICD_HDFD],0
						JNE		@f

; DO NOT install YSSCSICD.SYS
						OR		CS:[CONTROL_FLAGS],CONTROL_FLAG_DONT_INSTALL_YSSCSICD
						RET
; DO install YSSCSICD.SYS
@@:
						AND		CS:[CONTROL_FLAGS],(NOT CONTROL_FLAG_DONT_INSTALL_YSSCSICD)
						RET
SETUP_CONTROL_FLAGS_HD_FD_BOOT		ENDP
